desc: stereo rectifier by jeff robertson

slider1:0<0,2,1{Half,Full,Bypass}> left mode
slider2:1<0,1,1{Negative,Positive}> left sign

slider3:0<0,2,1{Half,Full,Bypass}> right mode
slider4:1<0,1,1{Negative,Positive}> right sign

slider5:0<-1,1,0.01> midpoint


@init
mode_half=0;
mode_full=1;
mode_bypass=2;


@slider
modeleft = slider1;
signleft = slider2;
moderight = slider3;
signright = slider4;
midpoint = slider5;

bias = abs(midpoint) * 2;


@sample

spl0= modeleft == mode_bypass ? spl0 :
      modeleft == mode_full ?  
	( signleft ?
	    max(spl0,midpoint)-min(spl0-midpoint,0) - bias
	  : 
	    min(spl0,midpoint)-max(spl0-midpoint,0) + bias 
	)
      :
	(signleft ? 
	    max(spl0, midpoint) - bias
	  : 
	    min(spl0, midpoint) + bias
	);


spl1= moderight == mode_bypass ? spl1 :
      moderight == mode_full ?  
	( signright ?
	    max(spl1,midpoint)-min(spl1-midpoint,0) - bias
	  : 
	    min(spl1,midpoint)-max(spl1-midpoint,0) + bias 
	)
      :
	(signright ? 
	    max(spl1, midpoint) - bias
	  : 
	    min(spl1, midpoint) + bias
	);
